plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.4'
	id 'io.spring.dependency-management' version '1.1.4'
	id "org.openapi.generator" version "7.4.0"
}

group = 'delivery.hooray'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '21'
}

repositories {
	mavenCentral()
}

ext {
	set('springdocVersion', '1.6.14')
	set('swaggerUiVersion', '5.3.1')
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.data:spring-data-commons'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation "org.springdoc:springdoc-openapi-ui:${springdocVersion}"
	implementation 'com.google.code.findbugs:jsr305:3.0.2'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
	implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'com.fasterxml.jackson.core:jackson-databind'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.boot:spring-boot-dependencies:2.7.15"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

openApiGenerate {
	generatorName = "spring"
	inputSpec = "$rootDir/services/customer/telegram-adapter/src/main/resources/openapi.yaml".toString()
	outputDir = "$buildDir/generated".toString()
	apiPackage = "delivery.hooray.telegramadapter.api"
	modelPackage = "delivery.hooray.telegramadapter.model"
	invokerPackage = "delivery.hooray.telegramadapter.invoker"
	configOptions = [
			interfaceOnly : "true",
			useTags: "true",
			useBeanValidation: "true",
			performBeanValidation: "true",
			dateLibrary: "java8"
	]
}

sourceSets {
	main {
		java {
			srcDirs 'src/main/java', "$buildDir/generated/src/main/java"
		}
	}
}

tasks.named('compileJava').configure {
	dependsOn tasks.named('openApiGenerate')
}

jar {
	archiveBaseName.set('openapi-spring')
	archiveVersion.set('1.0.0')
	manifest {
		attributes(
				'Implementation-Title': 'openapi-spring',
				'Implementation-Version': archiveVersion.get()
		)
	}
}
